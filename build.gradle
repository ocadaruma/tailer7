buildscript {
    ext {
        lombokVersion = '1.18.12'
    }
}

allprojects {
    group = 'com.mayreh'
    version = '0.1.1-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"

        testCompile 'junit:junit:4.12'
        testCompile 'org.assertj:assertj-core:3.11.1'
    }

    ext {
        isReleaseVersion = !version.endsWith('-SNAPSHOT')
    }
}

project(':tailer7') {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    afterEvaluate {
        publishing {
            repositories {
                maven {
                    if (isReleaseVersion) {
                        url("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
                    } else {
                        url("https://oss.sonatype.org/content/repositories/snapshots/")
                    }
                    credentials {
                        username findProperty("sonatypeUsername")
                        password findProperty("sonatypePassword")
                    }
                }
            }
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    afterEvaluate {
                        artifactId = project.name

                        artifact javadocJar
                        artifact sourcesJar

                        pom {
                            name = 'tailer7'
                            description = 'A scalable tailer powered by Redis'
                            url = 'https://github.com/ocadaruma/tailer7'

                            scm {
                                connection = 'scm:git:git@github.com:ocadaruma/tailer7.git'
                                url = 'git@github.com:ocadaruma/tailer7.git'
                            }

                            licenses {
                                license {
                                    name = 'The Apache License, Version 2.0'
                                    url = 'https://raw.githubusercontent.com/ocadaruma/tailer7/master/LICENSE'
                                }
                            }

                            developers {
                                developer {
                                    id = 'ocadaruma'
                                    name = 'Haruki Okada'
                                }
                            }
                        }
                    }
                }
            }
        }
        signing {
            required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
            sign publishing.publications.mavenJava
        }
    }
}
